# ~~~
# - Config file for the PcapPlusPlus package
# It defines the following variables
#  PcapPlusPlus_INCLUDE_DIR  - include directories for PcapPlusPlus
# ~~~

@PACKAGE_INIT@

list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/modules")

# Default behavior is to enable the old style include by default during the deprecation period.
if(NOT DEFINED PCAPPP_ENABLE_OLD_STYLE_INCLUDE)
  set(PCAPPP_ENABLE_OLD_STYLE_INCLUDE ON)
endif()

if(PCAPPP_ENABLE_OLD_STYLE_INCLUDE)
  message(
    DEPRECATION
    "PcapPlusPlus: Old style include directories are enabled. "
    "This functionality is deprecated and will be removed in the future. "
    "New include format should follow: \"#include <pcapplusplus/xxx.h>\" "
    "This behaviour can be disabled by setting \"PCAPPP_ENABLE_OLD_STYLE_INCLUDE\" to OFF"
  )
endif()

include(CMakeFindDependencyMacro)

set_and_check(PcapPlusPlus_INCLUDE_DIR
              ${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_INCLUDEDIR@/pcapplusplus)

if(UNIX)
  set(THREADS_PREFER_PTHREAD_FLAG ON)
endif()
find_dependency(Threads)

list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")

@_PCAPPP_CONFIG_DEPENDENCY@

include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")

check_required_components(@PROJECT_NAME@)
